plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "xyz.jpenilla.run-paper" version "2.3.1"
}

runServer {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }
    minecraftVersion "1.21.4"
}

processResources {
    expand("version": version)
}

dependencies {
    compileOnly "me.clip:placeholderapi:2.11.6" // Placeholder support
    implementation "com.google.code.gson:gson:2.10.1" // JSON parsing
    implementation "org.bstats:bstats-bukkit:3.0.2" // Plugin stats
    implementation "com.github.retrooper:packetevents-spigot:2.7.0" // Packets
    implementation "space.arim.dazzleconf:dazzleconf-ext-snakeyaml:1.2.1" // Configs
    implementation "lol.pyr:director-adventure:2.1.2" // Commands

    // Fancy text library
    implementation "net.kyori:adventure-platform-bukkit:4.3.4"
    implementation "net.kyori:adventure-text-minimessage:4.17.0"

    implementation project(":api")
}

ext {
    gitBranch = System.getenv('GIT_BRANCH') ?: ''
    gitCommitHash = System.getenv('GIT_COMMIT') ?: ''
    buildId = System.getenv('BUILD_ID') ?: ''
}

shadowJar {
    archivesBaseName = "ZNPCsPlus"
    archiveClassifier.set ""

    manifest {
        if (gitBranch?.trim()) {
            attributes('Git-Branch': gitBranch)
        }
        if (gitCommitHash?.trim()) {
            attributes('Git-Commit': gitCommitHash)
        }
        if (buildId?.trim()) {
            attributes('Build-Id': buildId)
        }
    }

    relocate "org.objectweb.asm", "lol.pyr.znpcsplus.libraries.asm"
    relocate "me.lucko.jarrelocator", "lol.pyr.znpcsplus.libraries.jarrelocator"

    relocate "org.bstats", "lol.pyr.znpcsplus.libraries.bstats"
    relocate "net.kyori", "lol.pyr.znpcsplus.libraries.kyori"
    relocate "org.checkerframework", "lol.pyr.znpcsplus.libraries.checkerframework"
    relocate "com.google.gson", "lol.pyr.znpcsplus.libraries.gson"
    relocate "com.github.retrooper.packetevents", "lol.pyr.znpcsplus.libraries.packetevents.api"
    relocate "io.github.retrooper.packetevents", "lol.pyr.znpcsplus.libraries.packetevents.impl"
    relocate "org.yaml.snakeyaml", "lol.pyr.znpcsplus.libraries.snakeyaml"
    relocate "space.arim.dazzleconf", "lol.pyr.znpcsplus.libraries.dazzleconf"
    relocate "lol.pyr.director", "lol.pyr.znpcsplus.libraries.command"

    minimize()
}

tasks.assemble.dependsOn shadowJar
